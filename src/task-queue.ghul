namespace Tester is
    use IO.Std;

    use System.Threading.Tasks.Task;
    use System.Threading.Tasks.Task`1;

    use Collections.Iterable;
    use Collections.LIST;

    class TASK_QUEUE is
        _max_tasks: int;

        _running_tasks: LIST[Task`1[Result]];

        init(max_tasks: int) is
            _max_tasks = max_tasks;

            _running_tasks = new LIST[Task`1[Result]](max_tasks);
        si

        queue(task: () -> Result) -> Iterable[Result] is
            let result = wait_any();

            let new_task = new Task`1[Result](task);

            new_task.start();

            _running_tasks.add(new_task);

            return result;
        si

        drain() -> Iterable[Result] is
            if _running_tasks.count == 0 then
                return null;
            fi

            return wait_all();
        si

        wait_any() -> Iterable[Result] => wait(false);
        wait_all() -> Iterable[Result] => wait(true);

        wait(all: bool) -> Iterable[Result] is
            let result = new LIST[Result]();

            if !all /\ _running_tasks.count < _max_tasks then
                return result;
            fi

            Task.wait_any(_running_tasks| .map((t: Task`1[Result]) -> Task => t).collect_array());

            let new_tasks = new LIST[Task`1[Result]]();

            for task in _running_tasks do
                if task.is_completed then
                    result.add(task.result);
                else
                    new_tasks.add(task);
                fi                
            od

            _running_tasks = new_tasks;

            return result;
        si
    si
si

